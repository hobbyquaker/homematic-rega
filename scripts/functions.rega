!# functions.rega
!#
!# Dieses Script gibt eine Liste der Gewerke im JSON Format aus
!#
!# 9'2017 hobbyquaker
!# 6'2013-9'2013 bluefox, hobbyquaker
!#

object  oFunction;
string  sFunctionId;
string  sChannelId;
boolean bFirst       = true;
boolean bFirstSecond = true;

Write("[");

foreach (sFunctionId, dom.GetObject(ID_FUNCTIONS).EnumUsedIDs()) {

    if (bFirst == false) {
        WriteLine(',');
    } else {
        bFirst = false;
    }

    oFunction = dom.GetObject(sFunctionId);
    Write('{"id": ' # sFunctionId # ', "name": "' # oFunction.Name() # '", "channels": [');
    bFirstSecond = true;

    foreach(sChannelId, oFunction.EnumUsedIDs()) {
        if (bFirstSecond == false) {
            Write(',');
        } else {
            bFirstSecond = false;
        }
        Write(sChannelId);
    }

    Write(']}');
}

Write(']');
